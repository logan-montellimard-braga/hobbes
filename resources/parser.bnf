<ROOT>       ::= (TPARAGRAPH
                  | PARAGRAPH
                  | HEADER
                  | QUOTE
                  | DEF
                  | ULIST
                  | OLIST
                  | CODE
                  | EX
                  | IMG
                  | VIDEO
                  | RULE)*

HEADER       ::= HEADERSYM <WHITESPACE> LINE <BLANKLINE>?
<HEADERSYM>  ::= H1 | H2 | H3 | H4 | H5 | H6
H1           ::= <"_" HBASE "1"?>
H2           ::= <"_s" HBASE     | "_" HBASE "2">
H3           ::= <"_ss" HBASE    | "_" HBASE "3">
H4           ::= <"_sss" HBASE   | "_" HBASE "4">
H5           ::= <"_ssss" HBASE  | "_" HBASE "5">
H6           ::= <"_sssss" HBASE | "_" HBASE "6">
<HBASE>      ::= #"(?i)t(i(t(r(e)?)?)?)?"

QUOTE        ::= <QUOTESYM> <WHITESPACE> CITE <SEP> LINE+ <BLANKLINE>+
CITE         ::= #"[^:]+"
<QUOTESYM>   ::= #"_(?i)c(i(t(a(t(i(o(n)?)?)?)?)?)?)?"

DEF          ::= <DEFSYM> <WHITESPACE> DEFTITLE <SEP> LINE+ <BLANKLINE>+
DEFTITLE     ::= #"[^:]+"
<DEFSYM>     ::= #"_(?i)d(e(f(i(n(i(t(i(o(n)?)?)?)?)?)?)?)?)?"

CODE         ::= <CODESYM> <WHITESPACE>? <EOL> CODELINE+ <BLANKLINE>+
<CODESYM>    ::= #"_(?i)c(o(d(e)?)?)?"
CODELINE     ::= WORD (WHITESPACE WORD)* <EOL>

EX           ::= <EXSYM> <WHITESPACE> LINE+ <BLANKLINE>+
<EXSYM>      ::= #"(?i)_e(x(e(m(p(l(e)?)?)?)?)?)?"

IMG          ::= <IMGSYM> <WHITESPACE> LINE <BLANKLINE>?
<IMGSYM>     ::= #"(?i)_img" | #"_(?i)i(m(a(g(e)?)?)?)?"

VIDEO        ::= <VIDEOSYM> <WHITESPACE> LINE <BLANKLINE>?
<VIDEOSYM>   ::= #"(?i)_v(i(d(e(o)?)?)?)?"

ULIST        ::= ULISTLINE+ <BLANKLINE>+
ULISTLINE    ::= <ULISTSYM> <WHITESPACE>+ WORD (WHITESPACE WORD)* <EOL>
<ULISTSYM>   ::= '+'| '*' | '-' | ':' | '%' | '$' | '&' | '~' | '#'

OLIST        ::= OLISTLINE+ BLANKLINE+
OLISTLINE    ::= <OLISTSYM> <WHITESPACE>+ WORD (WHITESPACE WORD)* EOL
<OLISTSYM>   ::= <#'[0-9]+\.'>

RULE         ::= <RULESYM> <RULESYM> <RULESYM> <RULESYM>* <BLANKLINE>+
<RULESYM>    ::= '+'| '*' | '-' | ':' | '%' | '$' | '&' | '~' | '#' | "'"

PARAGRAPH    ::= LINE+ BLANKLINE+
TPARAGRAPH   ::= LINE

<BLANKLINE>  ::= WHITESPACE* EOL
<LINE>       ::= (<TAB> | WORD) (WHITESPACE WORD)* EOL
<WHITESPACE> ::= (SPACE | TAB)+
<SPACE>      ::= ' '
<TAB>        ::= <'\t'>
<SEP>        ::= <WHITESPACE>? ':' <WHITESPACE>?
<WORD>       ::= #'\S+'
EOL          ::= <'\r'> | <'\n'> | <'\r\n'>
